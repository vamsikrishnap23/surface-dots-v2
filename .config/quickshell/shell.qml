import QtQuick
import Quickshell
import Quickshell.Wayland
import Quickshell.Io
import Quickshell.Hyprland
import "lib" as Lib
import "bars" as Bars
import "dock" as Dock
import "desktop" as Desktop
import "hub" as SystemHub

ShellRoot {
    Variants {
        model: Quickshell.screens
        Scope {
            id: v
            property var modelData

            readonly property bool topStyle: Lib.Configuration.barStyle === "top"

            Lib.ThemeEngine {
                id: screenTheme
            }

            Desktop.ScreenBorder {
                id: border
                screen: v.modelData
                visible: !v.topStyle
                theme: screenTheme
            }

            // Held until the settings load
            Loader {
                id: barLoader
                active: Lib.Configuration.ready
                sourceComponent: v.topStyle ? topBarComponent : taskBarComponent
            }

            Component {
                id: taskBarComponent
                Bars.TaskBar {
                    screen: v.modelData
                    drawerOpen: appDrawer.isOpen || wideDrawer.isOpen
                    onHasWindowsChanged: border.setTopSidesVisible(!hasWindows)
                    onLauncherClicked: isDockMode ? appDrawer.toggle() : wideDrawer.toggle()
                    onRequestHubToggle: v.toggleHub()
                }
            }

            // The top bar uses the custom TopLauncher
            Component {
                id: topBarComponent
                Bars.TopBar {
                    screen: v.modelData
                    onRequestHubToggle: v.toggleHub()
                    onRequestLauncherToggle: topLauncher.toggle()
                }
            }

            // Bar items that toggle an overlay, so each overlay can leave a hole
            // in its input region over its own toggle
            readonly property var taskBar: (!v.topStyle && barLoader.item) ? barLoader.item : null
            readonly property rect launcherHole: taskBar ? taskBar.launcherRect : Qt.rect(0, 0, 0, 0)
            readonly property rect clockHole: taskBar ? taskBar.clockRect : Qt.rect(0, 0, 0, 0)

            SystemHub.HubWindow {
                id: hubWindow
                screen: v.modelData
                visible: false
                toggleHole: v.clockHole
            }

            // Display picker, laptop panel only so it does not appear twice
            Loader {
                id: monitorPromptLoader
                active: v.modelData && v.modelData.name === Lib.MonitorService.promptOutput
                sourceComponent: Lib.MonitorPrompt {
                    theme: screenTheme
                    screen: v.modelData
                    onMoreOptionsRequested: {
                        hubWindow.showMonitors()
                        hubWindow.visible = true
                    }
                }
            }

            Connections {
                target: Lib.MonitorService
                function onGuestConnected(mon) {
                    if (monitorPromptLoader.item) monitorPromptLoader.item.open(mon)
                }
                function onKnownConnected(mon) {
                    if (monitorPromptLoader.item) monitorPromptLoader.item.notify(mon)
                }
                // Unplugged while the card was still up
                function onMonitorGone(name) {
                    var item = monitorPromptLoader.item
                    if (item && item.mon && item.mon.name === name) item.close()
                }
            }

            Lib.BrightnessOSD {
                theme: screenTheme
                screen: v.modelData
            }
            Lib.MicOSD {
                theme: screenTheme
                screen: v.modelData
            }
            Lib.ClipboardMenu {
                id: clipboardMenu
                theme: screenTheme
            }
            Lib.VolumeOSD {
                theme: screenTheme
                screen: v.modelData
            }
            Lib.ThemeOSD {
                theme: screenTheme
                screen: v.modelData
            }

            Dock.Drawer {
                id: appDrawer
                isDarkMode: screenTheme.isDarkMode
                launcherHole: v.launcherHole
            }

            Dock.WideDrawer {
                id: wideDrawer
                theme: screenTheme
                launcherHole: v.launcherHole
            }

            Dock.TopLauncher {
                id: topLauncher
                theme: screenTheme
            }

            function toggleHub() {
                if (hubWindow.visible) {
                    // Route through closeAll() so the exit animation plays before hiding
                    hubWindow.closeAll()
                } else {
                    // HubWindow.onVisibleChanged grabs keyboard focus on its inner
                    // Item; PanelWindow itself has no forceActiveFocus().
                    hubWindow.visible = true
                }
            }

            GlobalShortcut {
                name: "clipboardToggle"
                description: "Toggle clipboard history"
                onPressed: clipboardMenu.toggle()
            }
            GlobalShortcut {
                name: "hubToggle"
                description: "Toggle hub"
                onPressed: v.toggleHub()
            }

            GlobalShortcut {
                name: "drawerToggle"
                description: "Toggle app drawer"
                onPressed: {
                    if (v.topStyle) topLauncher.toggle()
                    else wideDrawer.toggle()
                }
            }

            // Picker if an unconfigured screen is waiting, otherwise the hub panel
            GlobalShortcut {
                name: "monitorPicker"
                description: "Display layout"
                onPressed: {
                    var pending = Lib.MonitorService.pending
                    if (pending && monitorPromptLoader.item) {
                        monitorPromptLoader.item.open(pending)
                    } else {
                        hubWindow.showMonitors()
                        if (!hubWindow.visible) hubWindow.visible = true
                    }
                }
            }
        }
    }
}
